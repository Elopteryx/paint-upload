apply plugin: "java"

ext.archivesBaseName = "paint-upload"
ext.appName = "%APP_NAME%"

task javadocJar(type: Jar) {
    classifier = "javadoc"
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = "sources"
    from sourceSets.main.allSource
}

artifacts {
    archives jar
    archives sourcesJar
    archives javadocJar
}

sourceSets {
    main {
        compileClasspath += configurations.provided
    }
    test {
        compileClasspath += configurations.provided
        runtimeClasspath += configurations.provided
    }
}

idea {
    module {
        scopes.PROVIDED.plus += [configurations.provided]
    }
}

javadoc {
    afterEvaluate {
        classpath += configurations.provided
        configure(options) {
            windowTitle "Paint Upload API Documentation"
            docTitle "Paint Upload API Documentation"
            bottom = 'Copyright &#169; 2015 <a href="https://github.com/Elopteryx">Creative Elopteryx</a>'
            breakIterator = true
            author = false
            source = "1.8"
            encoding = "UTF-8"
            docEncoding = "UTF-8"
            failOnError = true
            links = [ "http://jsr-305.googlecode.com/svn/trunk/javadoc",
                      "http://download.oracle.com/javase/8/docs/api/",
                      "http://download.oracle.com/javaee/7/api/",
            ]
        }
    }
}

dependencies {

    /* Servlet API. */
    provided "javax.servlet:javax.servlet-api:3.1.0"

    /* Restful web services API and implementation. */
    provided "javax.ws.rs:javax.ws.rs-api:2.0.1"

    /* Annotations for code documentation. */
    provided "com.google.code.findbugs:jsr305:3.0.0"

    /* Test runner. */
    testCompile "junit:junit:4.12"

    /* Http client library. */
    testCompile "org.apache.httpcomponents:httpclient:4.4"
    testCompile "org.apache.httpcomponents:httpmime:4.4"

    /* Http server. */
    testCompile "io.undertow:undertow-core:1.1.2.Final"
    testCompile "io.undertow:undertow-servlet:1.1.2.Final"

    /* Object mocking. */
    testCompile "org.mockito:mockito-core:2.0.4-beta"

}

publishing {
    publications {
        maven(MavenPublication) {
            groupId "com.github.elopteryx"
            artifactId "paint-upload"

            from components.java
        }
    }
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

//            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
//                authentication(userName: ossrhUsername, password: ossrhPassword)
//            }
//
//            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
//                authentication(userName: ossrhUsername, password: ossrhPassword)
//            }

            pom.scopeMappings.with {
                addMapping(1, configurations.provided, "provided")
            }

            pom.project {
                name "Paint Upload"
                groupId "com.github.elopteryx"
                artifactId "paint-upload"

                description "Paint Upload is an asynchronous multipart parser for servlets with version 3.1 or above." +
                        "Version 2.x releases are compiled to run on Java 8 or above."
                url "http://elopteryx.github.io/paint-upload-site"

                scm {
                    connection "scm:git:git@github.com/Elopteryx/paint-upload.git"
                    developerConnection "scm:git:git@github.com/Elopteryx/paint-upload.git"
                    url "scm:git:git@github.com/Elopteryx/paint-upload.git"
                }

                licenses {
                    license {
                        name "The Apache License, Version 2.0"
                        url "http://www.apache.org/licenses/LICENSE-2.0.txt"
                    }
                }

                developers {
                    developer {
                        id "elopteryx"
                        name "Adam Forgacs"
                        email "creative.elopteryx@gmail.com"
                    }
                }
            }
        }
    }
}
