ext.moduleName = 'com.github.elopteryx.upload.rs'

dependencies {

    /* Upload parser. */
    compile project(':upload-parser-core')

    /* Servlet API. */
    compileOnly "javax.servlet:javax.servlet-api:$servletApiVersion"

    /* Restful web services API. */
    compileOnly "javax.ws.rs:javax.ws.rs-api:$jaxrsApiVersion"

}

compileJava {
    inputs.property('moduleName', moduleName)
    doFirst {
        options.compilerArgs = [
                '--module-path', classpath.asPath,
        ]
        classpath = files()
    }
}

javadoc {
    options.addStringOption('-module-path', classpath.asPath)
}

jar {
    manifest {
        attributes(
                'Created-By': 'Creative Elopteryx',
                'Class-Path': configurations.compile.collect { it.getName() }.join(' '),
                'Automatic-Module-Name': 'com.github.elopteryx.upload.rs',
                'Implementation-Version': version
        )
    }
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
            afterEvaluate {
                groupId 'com.github.elopteryx'
                artifactId 'upload-parser-jaxrs'
            }
        }
    }
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment {
                MavenDeployment deployment -> signing.signPom(deployment)
            }

            repository(url: 'https://oss.sonatype.org/service/local/staging/deploy/maven2/') {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: 'https://oss.sonatype.org/content/repositories/snapshots/') {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.scopeMappings.with {
                addMapping(1, configurations.compileOnly, 'provided')
            }

            pom.project {
                name 'Upload Parser Jax-Rs'
                groupId 'com.github.elopteryx'
                artifactId 'upload-parser-jaxrs'

                description 'Upload Parser Jax-Rs is a file upload library for Jax-Rs applications.'
                url 'https://github.com/Elopteryx/upload-parser'

                scm {
                    connection 'scm:git:git@github.com/Elopteryx/upload-parser.git'
                    developerConnection 'scm:git:git@github.com/Elopteryx/upload-parser.git'
                    url 'scm:git:git@github.com/Elopteryx/upload-parser.git'
                }

                licenses {
                    license {
                        name 'The Apache License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id 'elopteryx'
                        name 'Adam Forgacs'
                        email 'creative.elopteryx@gmail.com'
                    }
                }
            }
        }
    }
}
