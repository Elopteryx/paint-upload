ext.moduleName = 'com.github.elopteryx.upload'

dependencies {

    /* Servlet API. */
    compileOnly("javax.servlet:javax.servlet-api:$servletApiVersion")

}

compileJava {
    inputs.property('moduleName', moduleName)
    doFirst {
        options.compilerArgs = [
                '--module-path', classpath.asPath,
        ]
        classpath = files()
    }
}

javadoc {
    options.addStringOption('-module-path', classpath.asPath)
}

jar {
    manifest {
        attributes(
                'Created-By': 'Creative Elopteryx',
                'Class-Path': configurations.compileClasspath.collect { it.getName() }.join(' '),
                'Automatic-Module-Name': 'com.github.elopteryx.upload',
                'Implementation-Version': archiveVersion
        )
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'upload-parser'
            from components.java
            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }
            pom {
                name = 'Upload Parser'
                groupId = 'com.github.elopteryx'
                artifactId = 'upload-parser'

                description = 'Upload Parser is an asynchronous file upload library for servlets.'
                url = 'https://github.com/Elopteryx/upload-parser'

                scm {
                    connection = 'scm:git:git@github.com/Elopteryx/upload-parser.git'
                    developerConnection = 'scm:git:git@github.com/Elopteryx/upload-parser.git'
                    url = 'scm:git:git@github.com/Elopteryx/upload-parser.git'
                }

                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id = 'elopteryx'
                        name = 'Adam Forgacs'
                        email = 'creative.elopteryx@gmail.com'
                    }
                }
            }
        }
    }
    repositories {
        maven {
            def releasesRepoUrl = 'https://oss.sonatype.org/service/local/staging/deploy/maven2/'
            def snapshotsRepoUrl = 'https://oss.sonatype.org/content/repositories/snapshots/'
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}
